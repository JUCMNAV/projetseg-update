package jucm_OneFileWorkaround::ucm::map;
require "platform:/lookup/aoUrnToRam/kermeta/aoUrnToIw/_Ref.kmt"

using kermeta::standard
using intermediateWorkflow

aspect class Stub {
	attribute _strategy:StubStrategy
	
	//LazyLoad because the creation of Stub is not under our control
	property readonly strategy:StubStrategy 
	getter is do
		if(_strategy.isVoid) then
			if(isRegularStub) then
				_strategy:=RegularStubStrategy.new.initialize(self)
			else
				_strategy:=PointcutStubStrategy.new.initialize(self)
			end 
		end
		result:=_strategy
	end
	
	method build() is do
		invokeBuildOnNodeConnections
		strategy.build
	end
	
	method link() is do
		invokeLinkOnSuccs
		strategy.link
	end
	
	method iwHasNodes():Boolean is do 
		result:=strategy.hasNodes 
	end
	
	method iwGetEntryNode(nodeConnection:NodeConnection):IwNode is do
		result:=strategy.entryNode(nodeConnection)
	end
	
	method iwGetExitNode(nodeConnection:NodeConnection):IwNode is do
		result:=strategy.exitNode(nodeConnection)
	end
	
	operation addIwPluginBinding(iwPluginBinding:IwPluginBinding) is do
		//PreCondition: isPointcutStub==false
		var iwStub:IwStub init iwGetEntryNode(void).asType(IwStub)
		iwStub.pluginBindings.add(iwPluginBinding)
	end
	
	property readonly isRegularStub:Boolean
	getter is do
		result:=isPointcutStub==false
	end
	
	property readonly isPointcutStub:Boolean
	getter is do
		result:=isRegularPointcutStub or isReplacementPointcutStub
	end
	
	property readonly isRegularPointcutStub:Boolean
	getter is do
		result:=aopointcut==PointcutKind.Regular
	end
	
	property readonly isReplacementPointcutStub:Boolean
	getter is do
		result:=aopointcut==PointcutKind.Replacement
	end
	
	property readonly stubNameOrPrefixId:String
	getter is do
		result:=nameOrPrefixId("Stub")
	end
}

