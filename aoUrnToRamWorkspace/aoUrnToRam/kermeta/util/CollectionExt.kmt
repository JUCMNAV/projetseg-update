package kermeta::standard;

require kermeta

using kermeta::standard

//@CannotWeaveAspectIntoGenericClass
//The best way to provide the operations of CollectionExt would have been to 
//write an aspect weave these operations directly in kermeta:standard::Collection.
//However, Kermeta does not support weaving aspect into generic classes. Another
//approach would have been to package the operations of CollectionExt in
//a static utility class. However, Kermeta does not supports static operations.
//See @StaticInKermeta. 
//To work around these problems, we take advantage of multiple inheritance.   
//Classes need to inherits from CollectionExt in order to use its operations.
//However, CollectionExt should be though as a mixin. That is, CollectionExt
//is not meant for instantiation. The only purpose of CollectionExt is to make
//its operations available to its subclasses. Moreover, CollectionExt does not 
//hold any state. In that sense, CollectionExt is very close to a static utility 
//class.
class CollectionExt{
	operation colExtMax(col:Collection<Integer>):Integer is do
		result:=0
		col.each{element|
			if(result.isVoid) then
				result:=element
			else
				if(result<element) then
					result:=element
				end
			end
		}
	end

	operation colExtSelectOfType<ORI_TYPE,CAST_TYPE>(source:Collection<ORI_TYPE>,dest:Collection<CAST_TYPE>) is do
		var filtredOut:Collection<ORI_TYPE> init source.select{element|element.isInstanceOf(CAST_TYPE)}
		colExtCast(filtredOut,dest)
	end

	operation colExtCast<ORI_TYPE,CAST_TYPE>(source:Collection<ORI_TYPE>,dest:Collection<CAST_TYPE>) is do
		dest.addAll(source.collect{element|element.asType(CAST_TYPE)})
	end
	
	operation colExtSingle<G>(col:Collection<G>):G
	is do
	    result:=colExtSelectSingle(col,function {notUsed:G|true})
	end

	operation colExtSelectSingle<G>(col:Collection<G>,selector:<G->Boolean>):G
	is do
		result:=_colExtSelectSequenceOfOne(col,selector).one
	end

	operation _colExtSelectSequenceOfOne<G>(col:Collection<G>,selector:<G->Boolean>):Sequence<G>
	post MustBeSingle_FoundEmpty is do
	    result.size>0
	end
	post MustBeSingle_FoundMany is do
	    result.size<2
	end
	is do
	    result:=col.select(selector)
	end
}
