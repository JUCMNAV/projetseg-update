package aoUrnToRam::test;

require kermeta
require "platform:/lookup/aoUrnToRam/kermeta/iwToIwLinkSteps.test/TestHelper.kmt"

using kermeta::standard
using kermeta::kunit
using intermediateWorkflow

class IwToIwLinkSteps_IwEndPoint_OneStubTestCase inherits TestCase,IwToIwLinkSteps_TestHelper
{
	reference concern:IwConcern
	reference workflow:IwWorkflow
	reference stubWorkflow:IwWorkflow
	reference currentStep:IwStep
	
	reference sut:IwEndPoint
	reference stub:IwStub
	reference stubExitNode:IwCustomizableNode
	
	method setUp() is do
		concern:= createIwConcern
		workflow:=createIwWorkflow.withConcern(concern)
		stubWorkflow:=createIwWorkflow.withConcern(concern)
		currentStep:=createIwStep.withConcern(concern)

		sut:=createIwEndPoint.withWorkflow(workflow)

		stub:=createIwStaticStub.withWorkflow(stubWorkflow)
		stubExitNode:=createIwCustomizableNode.withWorkflow(stubWorkflow)
		bindStaticStubToPluginEndPoint(stub,stubExitNode,sut)
	end
	
	
	@feature "FeaNamingOfRAMStep"
	operation testDeepFirstSearch_UnexploredWithStubDifferentStep_ShouldAddOutboundStubsToCurrentStep() is do
		var differentStep:IwStep init createIwStep.withConcern(concern)
		stub.step:=differentStep
		
		sut.step_DeepFirstSearch(currentStep)
		
		assertCollectionHasSingleElement(currentStep,stub.outboundInSteps)
	end
	
	@feature "FeaNamingOfRAMStep"
	operation testDeepFirstSearch_UnexploredWithStubAlreadyOutbound_ShouldAddOutboundStubsToCurrentStep() is do
		var differentStep:IwStep init createIwStep.withConcern(concern)
		stub.outboundInSteps.add(differentStep)
		
		sut.step_DeepFirstSearch(currentStep)
		
		assertCollectionHasTwoElements(currentStep,differentStep,stub.outboundInSteps)
	end
	
	@feature "FeaNamingOfRAMStep"
	operation testDeepFirstSearch_UnexploredWithStubFromOtherConcern_ShouldStopExploration() is do
		stubWorkflow.concern:=createIwConcern //Different concern
		
		sut.step_DeepFirstSearch(currentStep)

		assertVoid(stubExitNode.step)
		assertEquals(0,stub.outboundInSteps.size)		
	end

	@feature "FeaTransformAspectMarker"
	operation testDeepFirstSearch_UnexploredWithAspectMarker_ShouldStopExploration() is do
		stub.stubType:="aspectMarker"
		
		sut.step_DeepFirstSearch(currentStep)

		assertVoid(stubExitNode.step)
		assertEquals(0,stub.outboundInSteps.size)		
	end
	
}