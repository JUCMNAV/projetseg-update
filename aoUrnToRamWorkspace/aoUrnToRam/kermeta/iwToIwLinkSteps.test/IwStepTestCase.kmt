package aoUrnToRam::test;

require kermeta
require "platform:/lookup/aoUrnToRam/kermeta/iwToIwLinkSteps.test/TestHelper.kmt"

using kermeta::standard
using kermeta::kunit
using intermediateWorkflow

class IwToIwLinkSteps_IwStepTestCase inherits TestCase,IwToIwLinkSteps_TestHelper
{
	reference concern:IwConcern
	reference workflow:IwWorkflow
	reference toMerge:IwStep
	reference sut:IwStep
	
	method setUp() is do
		concern:= createIwConcern
	    workflow:=createIwWorkflow.withConcern(concern)
		toMerge:=createIwStep.withConcern(concern)
		toMerge.name:="ToMergeStepName"
		sut:=createIwStep.withConcern(concern)
		sut.name:="SutStepName"
	end
	
	@feature "FeaNamingOfRAMStep"
	operation testMerge_ShouldMergeStepNameInAlphabethicalOrder() is do
		var smallerStepName:IwStep init createIwStep
		smallerStepName.name:="a"
		sut.name:="b"
		toMerge.name:="c"
				
		sut.merge(toMerge)
		sut.merge(smallerStepName)
		
		assertEquals("a_b_c",sut.name)
	end
	
	@feature "FeaNamingOfRAMStep"
	operation testMerge_ShouldMergeNodes() is do
		linkTwoNodes(sut)
		linkTwoNodes(toMerge)
		
		sut.merge(toMerge)
		
		assertEquals(4,sut.nodes.size)
	end
	
	@feature "FeaNamingOfRAMStep"
	operation testMerge_ShouldMergeOutboundStub() is do
		bindTwoStub(sut)
		bindTwoStub(toMerge)
	
		sut.merge(toMerge)
		
		assertEquals(4,sut.outboundStubs.size)
	end
	
	@feature "FeaNamingOfRAMStep"
	operation testMerge_ShouldDeleteStepToMerged() is do
		linkTwoNodes(toMerge)
		toMerge.outboundStubs.add(createIwStub)
		toMerge.outboundStubs.add(createIwStub)
	
		sut.merge(toMerge)
		
		assertVoid(toMerge.concern)
		assertEquals(0,toMerge.nodes.size)
		assertEquals(0,toMerge.outboundStubs.size)
	end
	
	@feature "FeaNamingOfRAMStep"
	operation testMerge_ReactiveWithReactive_ShouldYieldReactiveStepWithBothNames() is do
		parameterizableTestMerge_StartupStep(false,"SutStepName_ToMergeStepName",false,false)
	end
	
	@feature "FeaNamingOfRAMStep"
	operation testMerge_ReactiveWithStartup_ShouldYieldReactiveStepWithReactiveName() is do
		parameterizableTestMerge_StartupStep(false,"SutStepName",false,true)
	end

	@feature "FeaNamingOfRAMStep"
	operation testMerge_StartupWithReactive_ShouldYieldReactiveStepWithReactiveName() is do
		parameterizableTestMerge_StartupStep(false,"ToMergeStepName",true,false)
	end

	@feature "FeaNamingOfRAMStep"
	operation testMerge_StartupWithStartup_ShouldYieldStartupStepWithBothNames() is do
		parameterizableTestMerge_StartupStep(true,"SutStepName_ToMergeStepName",true,true)
	end
	
	operation parameterizableTestMerge_StartupStep(expectedIsStartupStep:Boolean,expectedName:String,sutIsStartupStep:Boolean,toMergeIsStartupStep:Boolean) is do
		sut.isStartupStep:=sutIsStartupStep
		toMerge.isStartupStep:=toMergeIsStartupStep
	
		sut.merge(toMerge)
		
		assertEquals(expectedIsStartupStep,sut.isStartupStep)
		assertEquals(expectedName,sut.name)
	end

	operation bindTwoStub(step:IwStep) is do
		step.outboundStubs.add(createIwStub)
		step.outboundStubs.add(createIwStub)
	end
	
	operation linkTwoNodes(step:IwStep) is do
		step.nodes.add(createIwCustomizableNode)
		step.nodes.add(createIwCustomizableNode)
	end
}