package aoUrnToRam::test;

require "platform:/lookup/aoUrnToRam/kermeta/iwToJavaInstantiator.test/TestHelper.kmt"

using kermeta::standard
using kermeta::kunit
using intermediateWorkflow
using javaInstantiator

class IwToJavaInstantiator_IwNodeConnectionTestCase inherits TestCase,IwToJavaInstantiator_TestHelper
{
	reference sut:IwNodeConnection
	reference workflowInstantiator:WorkflowInstantiator
	
	method setUp() is do
		workflowInstantiator:=createWorkflowInstantiator
	end
	
	@feature "FeaWorkflowInstantiationWithoutStubs"
	operation testAppendLinkStatement_NodeToNode_ShouldLinkSourceToTarget() is do
		sut:=createIwCustomizableNode.connect(createIwEndPoint)
		
		sut.jiAppendAddNextNodeStatements(workflowInstantiator)
		
		assertEquals("        _theCustomizableNode.addNextNode(_theEndPoint);\n"
					,workflowInstantiator.content)
	end

	@feature "FeaWorkflowInstantiationWithStubs"
	operation testAppendLinkStatement_NodeToStub_ShouldLinkSourceToTargetInPath() is do
		sut:=createIwCustomizableNode.connect(createIwStub)
		sut.stubEntryIndex:=1
		
		sut.jiAppendAddNextNodeStatements(workflowInstantiator)
		
		assertEquals("        _theCustomizableNode.addNextNode(_theStub,\"1\");\n"
					,workflowInstantiator.content)
	end

	@feature "FeaWorkflowInstantiationWithStubs"
	operation testAppendLinkStatement_StubToNode_ShouldLinkSourceOutPathToTarget() is do
		sut:=createIwStub.connect(createIwCustomizableNode)
		sut.stubExitIndex:=1
		
		sut.jiAppendAddNextNodeStatements(workflowInstantiator)
		
		assertEquals("        _theStub.addNextNode(\"1\",_theCustomizableNode);\n"
					,workflowInstantiator.content)
	end

	@feature "FeaWorkflowInstantiationWithStubs"
	operation testAppendLinkStatement_StubToStub_ShouldLinkSourceOutPathToTargetInPath() is do
		sut:=createIwStub.connect(createIwStub)
		sut.target.name:="anotherStub"
		sut.stubExitIndex:=1
		sut.stubEntryIndex:=2
		
		sut.jiAppendAddNextNodeStatements(workflowInstantiator)
		
		assertEquals("        _theStub.addNextNode(\"1\",_anotherStub,\"2\");\n"
					,workflowInstantiator.content)
	end
	
	@feature "FeaWorkflowInstantiationWithoutStubs"
	operation testAppendLinkStatement_OrForkToNode_ShouldLinkSourceToTargetWithCondition() is do
		sut:=createIwOrFork.connect(createIwEndPoint).withDefaultCondition
		
		sut.jiAppendAddNextNodeStatements(workflowInstantiator)
		
		assertEquals("        _theOrFork.addNextNode(\"theCondition\",_theEndPoint);\n"
					,workflowInstantiator.content)
	end

	@feature "FeaWorkflowInstantiationWithStubs"
	operation testAppendLinkStatement_OrForkToStub_ShouldLinkSourceToTargetInPathWithCondition() is do
		sut:=createIwOrFork.connect(createIwStub).withDefaultCondition
		sut.stubEntryIndex:=1
		
		sut.jiAppendAddNextNodeStatements(workflowInstantiator)
		
		assertEquals("        _theOrFork.addNextNode(\"theCondition\",_theStub,\"1\");\n"
					,workflowInstantiator.content)
	end
}