package intermediateWorkflow;

require "platform:/lookup/aoUrnToRam/kermeta/iwToStepClassDiagram/_Ref.kmt"

using kermeta::standard
using ram
using kermeta::utils
using aoUrnToRam::util

aspect class IwStep inherits PathUtil {
	reference ramAspect:Aspect
	reference ramCustomizableNodeClasses:Hashtable<String,Class>
	reference ramCustomizableNodeMapping:Mapping
	reference ramReactiveSystemWorkflowInstantiation:Instantiation	
	reference ramWorkflowInstantiation:Instantiation
	
	//base classes
	reference ramInputDataBaseClass:Class
	reference ramInputDataBaseClassMapping:Mapping
	reference ramOutputDataBaseClass:Class
	reference ramOutputDataBaseClassMapping:Mapping

	property readonly ramStructuralView : ClassDiagram
	getter is do
		result := ramAspect.structuralView
	end
	
	property readonly customizableNodes: IwCustomizableNode[0..*]
	getter is do
		//stle: DRY
		//stle: refactor for reuse
		result:=nodes.select{node|node.isInstanceOf(IwCustomizableNode)}
			 .collect{node|node.asType(IwCustomizableNode)}.asOrderedSet
	end

/*********************************************************
Build
*********************************************************/
	operation build() is do
		buildRamAspect
		buildInstantiations
		buildMappings
		buildBaseClasses
		buildCustomizableNodeClasses
		nodes.each{node|node.build}
	end
	
	operation buildRamAspect() is do
		ramAspect:=Aspect.new
		ramAspect.name:=name
		ramAspect.structuralView:=ClassDiagram.new
	end
	
	operation buildInstantiations() is do
		ramReactiveSystemWorkflowInstantiation:=Instantiation.new
		ramWorkflowInstantiation:=Instantiation.new
	end
	
	operation buildMappings() is do
		ramCustomizableNodeMapping:=Mapping.new
	end
	
	operation buildBaseClasses() is do
		ramInputDataBaseClass:=createRamClass("InputData")
		ramOutputDataBaseClass:=createRamClass("OutputData")
	end

	//For rational see @RamCustomizableNodeClassBuildByIwStep  	
	operation buildCustomizableNodeClasses() is do
		ramCustomizableNodeClasses:=Hashtable<String,Class>.new
		customizableNodes.each{customizableNode|
			if(ramCustomizableNodeClasses.containsKey(customizableNode.name)==false) then
				ramCustomizableNodeClasses.put(customizableNode.name,createRamClass(customizableNode.name))
			end
		}
	end

	operation createRamClass(name:String):Class is do
		result:=Class.new
		result.name:=name
	end
	
/*********************************************************
Link
*********************************************************/
	
	operation link(ramWorkspace:RamWorkspace) is do
		linkCustomizableNodeClasses
		nodes.each{node|node.link}
		linkInstantiations(ramWorkspace)
		linkBaseClasses(ramWorkspace)
		addAspectToRamWorkspace(ramWorkspace)
	end

	operation linkCustomizableNodeClasses() is do
		ramCustomizableNodeClasses.values.each{ramClass|
			linkCustomizableNode(ramClass)
		}
	end
	
	operation linkInstantiations(ramWorkspace:RamWorkspace) is do
		ramReactiveSystemWorkflowInstantiation.externalAspect:=ramWorkspace.reactiveSystemWorkflow
		ramAspect.instantiations.add(ramReactiveSystemWorkflowInstantiation)

		ramWorkflowInstantiation.externalAspect:=ramWorkspace.workflow
		ramAspect.instantiations.add(ramWorkflowInstantiation)
		
		ramWorkflowInstantiation.mappings.add(ramCustomizableNodeMapping)
		ramCustomizableNodeMapping.sourceModelElement:=ramWorkspace.customizableNode
	end
	
	operation linkBaseClasses(ramWorkspace:RamWorkspace) is do
		linkRamInputDataBaseClass(ramWorkspace)
		linkRamOutputDataBaseClass(ramWorkspace)
	end

	operation addAspectToRamWorkspace(ramWorkspace:RamWorkspace) is do
		//Filter out empty aspect
		if(isEmtpyStepClassDiagram==false) then
			var workspacePath:String init combine("Steps",combine(concern.name,name))
			ramWorkspace.aspectPerWorkspacePath.put(workspacePath,ramAspect) 
		end
	end
	
	property isEmtpyStepClassDiagram:Boolean
	getter is do
		result:=ramStructuralView.classes.size==0
	end
	
	operation linkRamInputDataBaseClass(ramWorkspace:RamWorkspace) is do
		if(hasAtLeastOneInputClass) then
			linkClassToClassDiagram(ramInputDataBaseClass)
			ramInputDataBaseClassMapping:=mapExternalClass(
				ramInputDataBaseClass,
				ramWorkspace.inputData,
				ramReactiveSystemWorkflowInstantiation
			)
		end
	end
	
	property hasAtLeastOneInputClass:Boolean
	getter is do
		result:=nodes.exists{node|node.isInstanceOf(IwInput)}
	end
	
	operation linkRamOutputDataBaseClass(ramWorkspace:RamWorkspace) is do
		if(hasAtLeastOneOutputClass) then
			linkClassToClassDiagram(ramOutputDataBaseClass)
			ramOutputDataBaseClassMapping:=mapExternalClass(
				ramOutputDataBaseClass,
				ramWorkspace.outputData,
				ramReactiveSystemWorkflowInstantiation
			)
		end
	end
	
	property hasAtLeastOneOutputClass:Boolean
	getter is do
		result:=nodes.exists{node|node.isInstanceOf(IwOutput)}
	end
	
	operation mapExternalClass(ramClass:Class,externalRamClass:Class,instantiation:Instantiation):Mapping is do
		result:=Mapping.new
		result.sourceModelElement:=externalRamClass
		result.maps.add(ramClass)
		instantiation.mappings.add(result)
	end

/*********************************************************
Add Ram Class API
*********************************************************/
	operation addInputDataSubtype(subtype:Class) is do
		linkClassToClassDiagram(subtype)
		subtype.superTypes.add(ramInputDataBaseClass)
	end
	
	operation addOutputDataSubtype(subtype:Class) is do
		linkClassToClassDiagram(subtype)
		subtype.superTypes.add(ramOutputDataBaseClass)
	end
	
	operation linkCustomizableNode(customizableNode:Class) is do
		linkClassToClassDiagram(customizableNode)
		ramCustomizableNodeMapping.maps.add(customizableNode)
	end

	operation linkClassToClassDiagram(ramClass:Class) is do
		ramStructuralView.classes.add(ramClass)
	end
}
