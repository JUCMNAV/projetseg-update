package intermediateWorkflow;

require "platform:/lookup/aoUrnToRam/kermeta/iwToStepClassDiagram/_Ref.kmt"

using kermeta::standard
using ram
using kermeta::utils

aspect class IwStep inherits CollectionExt,RamFactory{
	reference ramAspect:Aspect
	reference ramLocalContextClass:Class
	reference ramRVoid:Type
	reference ramCustomizableNodeClasses:Hashtable<String,Class>
	reference ramCustomizableNodeMapping:Mapping
	reference ramExecuteOperationMapping:Mapping
	reference ramCustomizableInputDataMapping:Mapping
	reference ramCustomizableOutputDataMapping:Mapping
	reference ramInputInstantiation:Instantiation
	reference ramOutputInstantiation:Instantiation	
	reference ramWorkflowInstantiation:Instantiation
	
	property readonly ramStructuralView : StructuralView
	getter is do
		result := ramAspect.structuralView
	end
	
	property readonly customizableNodes: IwCustomizableNode[0..*]
	getter is do
		result:=OrderedSet<IwCustomizableNode>.new
		colExtSelectOfType(nodes,result)
	end

/*********************************************************
Build
*********************************************************/
	operation build() is do
		buildRamAspect
		buildInstantiations
		buildMappings
		buildCustomizableNodeClasses
		ramLocalContextClass:=createRamClass("LocalContext")
		ramRVoid:=createRVoid
		nodes.each{node|node.build}
	end
	
	operation buildRamAspect() is do
		ramAspect:=Aspect.new
		ramAspect.name:=name
		ramAspect.structuralView:=StructuralView.new
	end
	
	operation buildInstantiations() is do
		ramInputInstantiation:=createInstantiation_Extends
		ramOutputInstantiation:=createInstantiation_Extends
		ramWorkflowInstantiation:=createInstantiation_Extends
	end
	
	operation createInstantiation_Extends():Instantiation is do
		result:=Instantiation.new
		result.type:=InstantiationType.Extends
	end
	
	operation buildMappings() is do
		ramCustomizableNodeMapping:=Mapping.new
		ramExecuteOperationMapping:=Mapping.new
		ramCustomizableInputDataMapping:=Mapping.new
		ramCustomizableOutputDataMapping:=Mapping.new
	end	

	//For rational see @RamCustomizableNodeClassBuildByIwStep  	
	operation buildCustomizableNodeClasses() is do
		ramCustomizableNodeClasses:=Hashtable<String,Class>.new
		customizableNodes.each{customizableNode|
			if(ramCustomizableNodeClasses.containsKey(customizableNode.name)==false) then
				ramCustomizableNodeClasses.put(customizableNode.name,createRamCustomizableNodeClass(customizableNode.name))
			end
		}
	end
	
/*********************************************************
Link
*********************************************************/
	
	operation link(ramWorkspace:RamWorkspace) is do
		linkClassToClassDiagram(ramLocalContextClass)
		ramStructuralView.types.add(ramRVoid)
		linkCustomizableNodeClasses
		nodes.each{node|node.link}
		linkInstantiations(ramWorkspace)
		addAspectToRamWorkspace(ramWorkspace)
	end

	operation linkCustomizableNodeClasses() is do
		ramCustomizableNodeClasses.values.each{ramClass|
			linkCustomizableNode(ramClass)
		}
	end

	operation addAspectToRamWorkspace(ramWorkspace:RamWorkspace) is do
		//Filter out empty aspect
		if(isEmtpyStepClassDiagram==false) then
			var workspacePath:String init "Steps".uriAppend(concern.name).uriAppend(name)
			ramWorkspace.aspectPerWorkspacePath.put(workspacePath,ramAspect) 
		end
	end
	
	property isEmtpyStepClassDiagram:Boolean
	getter is do
		// by default a LocalContext class is generated all the time; at least one other class needs to exist
		result:=ramStructuralView.classes.size==1
	end
	
	operation linkInstantiations(ramWorkspace:RamWorkspace) is do
		linkWorkflowInstantiation(ramWorkspace)
		linkInputInstantiation(ramWorkspace)
		linkOutputInstantiation(ramWorkspace)
	end
	
	operation linkWorkflowInstantiation(ramWorkspace:RamWorkspace) is do
		linkInstantiation(ramWorkflowInstantiation,ramWorkspace.workflow)
		linkMapping(
			ramCustomizableNodeMapping,
			ramWorkspace.customizableNode,
			ramWorkflowInstantiation
		)
		linkMapping(
			ramExecuteOperationMapping,
			ramWorkspace.executeOperation,
			ramWorkflowInstantiation
		)
	end
	
	operation linkInputInstantiation(ramWorkspace:RamWorkspace) is do
		if(hasAtLeastOneInputClass) then
			linkInstantiation(ramInputInstantiation,ramWorkspace.inputAspect)
			linkMapping(
				ramCustomizableInputDataMapping,
				ramWorkspace.customizableInputData,
				ramInputInstantiation
			)
		end
	end
	
	operation linkOutputInstantiation(ramWorkspace:RamWorkspace) is do
		if(hasAtLeastOneOutputClass) then
			linkInstantiation(ramOutputInstantiation,ramWorkspace.outputAspect)
			linkMapping(
				ramCustomizableOutputDataMapping,
				ramWorkspace.customizableOutputData,
				ramOutputInstantiation
			)
		end
	end
	
	operation linkInstantiation(instantiation:Instantiation,externalAspect:Aspect) is do
		instantiation.externalAspect:=externalAspect
		ramAspect.instantiations.add(instantiation)
	end
	
	operation linkMapping(mapping:Mapping,fromElement:MappableElement,instantiation:Instantiation) is do
		mapping.fromElement:=fromElement
		instantiation.mappings.add(mapping)
	end

	property hasAtLeastOneInputClass:Boolean
	getter is do
		result:=nodes.exists{node|node.isInstanceOf(IwInput)}
	end
	
	property hasAtLeastOneOutputClass:Boolean
	getter is do
		result:=nodes.exists{node|node.isInstanceOf(IwOutput)}
	end
	
	operation mapExternalClass(ramClass:Class,externalRamClass:Class,instantiation:Instantiation):Mapping is do
		result:=Mapping.new
		result.fromElement:=externalRamClass
		result.toElements.add(ramClass)
		instantiation.mappings.add(result)
	end

/*********************************************************
Add Ram Class API
*********************************************************/
	operation linkInputData(inputData:Class) is do
		linkClassToClassDiagram(inputData)
		ramCustomizableInputDataMapping.toElements.add(inputData)
	end
	
	operation linkOutputData(outputData:Class) is do
		linkClassToClassDiagram(outputData)
		ramCustomizableOutputDataMapping.toElements.add(outputData)
	end
	
	operation linkCustomizableNode(customizableNode:Class) is do
		linkClassToClassDiagram(customizableNode)
		ramCustomizableNodeMapping.toElements.add(customizableNode)
		
		linkExecuteOperation(colExtSingle(customizableNode.operations))
	end
	
	operation linkExecuteOperation(executeOperation:Operation) is do
		ramExecuteOperationMapping.toElements.add(executeOperation)
		executeOperation.returnType:=ramRVoid

		var cParam:Parameter init colExtSingle(executeOperation.parameters)
		cParam.type:=ramLocalContextClass
	end

	operation linkClassToClassDiagram(ramClass:Class) is do
		ramStructuralView.classes.add(ramClass)
	end
}
