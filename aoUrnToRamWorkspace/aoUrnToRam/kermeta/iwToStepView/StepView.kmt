package intermediateWorkflow;

require "platform:/lookup/aoUrnToRam/kermeta/util/PathUtil.kmt"
require "platform:/lookup/aoUrnToRam/kermeta/iwToStepView/_Ref.kmt"

using kermeta::standard
using kermeta::utils

class StepView{
	attribute imgFolderWindowsPath: String
	attribute workspacePath: String
	attribute _dot:StringBuffer
	reference step:IwStep
	
	operation append(toAppend:String) is do
		_dot.append(toAppend)
	end
	
	operation appendLine(line:String) is do
		append(line) 
		append("\n")
	end
	
	operation appendImg(imageName:String) is do
		//stle: handle append windows folder properly
		append("<img src=\""+imgFolderWindowsPath+"\\"+imageName+"\"></img>") 
	end
	
	property readonly dot : String
	getter is do
		result:=_dot.toString
	end
	
	operation isPartOfStepView(node: IwNode):Boolean is do
		result:=(step==node.step)
	end
	
	attribute isStubsFromOtherConcernsPlaceholderRequired:Boolean
	operation appendStubsFromOtherConcernsPlaceholder() is do
		isStubsFromOtherConcernsPlaceholderRequired:=true
		append("StubsFromOtherConcernsPlaceholder_80C8D3AF_486D_4EB9_8A4A_B63B09537442")
	end
	
	operation appendStubFromOtherConcerPlaceholder() is do
		if(isStubsFromOtherConcernsPlaceholderRequired==true) then
			appendLine("")
			append("    ") 
			appendStubsFromOtherConcernsPlaceholder
			appendLine("[label=\"*\",style=dashed]")
		end
	end
	
	property readonly customizableClassColor : String
	getter is do
		result:="gray"
	end
	
	operation dotEscape(toEscape:String):String is do
		result:="\""+dotEscapeInner(toEscape)+"\""
	end
	
	operation dotEscapeInner(toEscape:String):String is do
		result:=toEscape.replace("\\","\\\\")
		result:=result.replace("\"","\\\"")
	end
	
}