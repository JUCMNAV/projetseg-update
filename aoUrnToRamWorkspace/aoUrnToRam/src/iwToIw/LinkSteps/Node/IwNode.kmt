package intermediateWorkflow;

require "platform:/lookup/aoUrnToRam/src/iwToIw/LinkSteps/_Ref.kmt"

using kermeta::standard

aspect class IwNode {
	operation step_DeepFirstSearch(currentStep:IwStep) is do
		if(isUnexplored) then
			explore(currentStep)
		else
			if(isPartOfStep(currentStep)==false) then
				currentStep.merge(step)
			end
		end
	end

	//virtual
	operation explore(currentStep:IwStep) is do
		step:=currentStep
		succs.each{nodeConnection|nodeConnection.target.step_DeepFirstSearch(step)}
	end
	
	operation isPartOfStep(currentStep:IwStep):Boolean is do
		result:=step==currentStep
	end
	
	property readonly isUnexplored : Boolean
	getter is do
		result:= step.isVoid
	end
	
	operation createStep(isStartupStep:Boolean):IwStep is do
		result:=IwStep.new
		result.name:=name
		result.isStartupStep:=isStartupStep
		concern.steps.add(result)
	end
}