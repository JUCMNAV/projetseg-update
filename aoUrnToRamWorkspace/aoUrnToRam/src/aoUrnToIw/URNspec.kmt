package oneurn::urn;
require "platform:/lookup/aoUrnToRam/src/aoUrnToIw/_Ref.kmt"

using kermeta::standard
using oneurn::urncore
using oneurn::ucm::map
using intermediateWorkflow

aspect class URNspec{
	reference iwModel: IwModel
	reference iwWithoutConcern:IwConcern
	
	//@UrnSpecNameWorkAround
	//URNspec already has a name property but it is always set to URNspec.
	//The property UrnSpecName is weaved into URNspec to work around this problem.
	//UrnSpecName is initialized from the .jucm file name 
	attribute urnSpecName:String
	
	operation toIw():IwModel is do
		build
		link
		result := iwModel
	end

//*********************************************************
//Build
//*********************************************************/
	operation build() is do
		ucmMaps.each{ucmMap|ucmMap.build}
		concerns.each{concern|concern.build}
		buildIwModel
		buildIwWithoutConcern
	end

	operation buildIwModel() is do
		iwModel:=IwModel.new
		iwModel.name:=urnSpecName
	end
	
	operation buildIwWithoutConcern() is do
		iwWithoutConcern:=IwConcern.new
		iwWithoutConcern.name:="WithoutConcern"
	end

//*********************************************************
//Link
//*********************************************************/
	operation link() is do
		ucmMaps.each{ucmMap|ucmMap.link}
		linkConcerns
	end
	
	operation linkConcerns() is do
		concerns.each{concern|concern.link}
		//do not link empty concern
		if(iwWithoutConcern.workflows.size>0) then
			iwWithoutConcern.model:=iwModel
		end
	end

//*********************************************************
//AoUrnToRam Extensions
//*********************************************************/
	property readonly ucmMaps : seq UCMmap[0..*]
	getter is do
		//stle: DRY
		result:= urndef.specDiagrams.select{specDiagram|specDiagram.isInstanceOf(UCMmap)}
						            .collect{specDiagram|specDiagram.asType(UCMmap)}
	end
	
	property readonly concerns : oset Concern[0..*]
	getter is do
		result:= urndef.concerns
	end
	
}

