package oneurn::ucm::map;
require "platform:/resource/aoUrnToRam/src/aoUrnToIw/_Ref.kmt"

using kermeta::standard
using kermeta::utils
using intermediateWorkflow

class PointcutStubStrategy inherits StubStrategy {
	reference iwEndPoints:Hashtable<NodeConnection,IwEndPoint>
	reference iwStartPoints:Hashtable<NodeConnection,IwStartPoint>

	method build() is do
		buildIwEndPoints
		buildIwStartPoints
    end
    
    operation buildIwEndPoints() is do
    	iwEndPoints:=Hashtable<NodeConnection,IwEndPoint>.new

    	var index:Integer init 1
		stub.predAsNodeConnection.each{nodeConnection|
			var iwEndPoint:IwEndPoint init IwEndPoint.new
			iwEndPoint.name:=stub.name+"_In"+index.toString
			iwEndPoints.put(nodeConnection,iwEndPoint)
			index:=index+1
		}
    end
    
    operation buildIwStartPoints() is do
    	iwStartPoints:=Hashtable<NodeConnection,IwStartPoint>.new

    	var index:Integer init 1
		stub.succAsNodeConnection.each{nodeConnection|
			iwStartPoints.put(nodeConnection,buildIwStartPoint(nodeConnection,index))
			index:=index+1
		}
    end
    
    operation buildIwStartPoint(nodeConnection:NodeConnection,index:Integer):IwStartPoint is do
    	result:=IwStartPoint.new
		result.name:=stub.name+"_Out"+index.toString
		if(nodeConnection.inBindingsPlugin.size>0) then
			result.startPointType:="bound"
		else
			result.startPointType:="global"
		end
    end

	method link() is do
		linkIwEndPointsToIwWorkflow
		linkIwStartPointsToIwWorkflow
	end
	
	operation linkIwStartPointsToIwWorkflow() is do
		iwStartPoints.values.each{iwStartPoint|
			stub.ucmMap.iwWorkflow.nodes.add(iwStartPoint)
			stub.ucmMap.iwWorkflow.startPoints.add(iwStartPoint)
		}
	end
	
	operation linkIwEndPointsToIwWorkflow() is do
		if(stub.isReplacementPointcutStub==false) then
			iwEndPoints.values.each{iwEndPoint|iwEndPoint.workflow:=stub.ucmMap.iwWorkflow}
		end
	end

	method hasNodes():Boolean is do
		result:=iwEndPoints.size>0 or iwStartPoints.size>0
    end

	method entryNode(nodeConnection:NodeConnection):IwNode is do
		result:=iwEndPoints.getValue(nodeConnection)
    end

	method exitNode(nodeConnection:NodeConnection):IwNode is do
		result:=iwStartPoints.getValue(nodeConnection)
    end
}

