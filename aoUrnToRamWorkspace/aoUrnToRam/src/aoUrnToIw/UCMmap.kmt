package oneurn::ucm::map;
require "platform:/resource/aoUrnToRam/src/aoUrnToIw/_Ref.kmt"

using kermeta::standard
using intermediateWorkflow
using oneurn::urn

aspect class UCMmap{
	reference iwWorkflow: IwWorkflow

//*********************************************************
//Build
//*********************************************************/
	operation build() is do
		pathNodes.each{pathNode|pathNode.build}

		iwWorkflow:=IwWorkflow.new
		iwWorkflow.name:=nameOrPrefixId("Workflow")
	end

//*********************************************************
//Link
//*********************************************************/
	operation link() is do
		linkWorkflowToConcern
		pathNodes.each{pathNode|pathNode.link}
	end

	operation linkWorkflowToConcern() is do
		if(concern.isVoid) then
			iwWorkflow.concern:=urnSpec.iwWithoutConcern
		else
			iwWorkflow.concern:=concern.iwConcern
		end	
	end	

//*********************************************************
//AoUrnToRam Extensions
//*********************************************************/
	property readonly pathNodes : seq PathNode[0..*]
	getter is do
		//stle: DRY
		//stle: refactor for reuse
		result:=nodes.select{node|node.isInstanceOf(PathNode)}
			 .collect{node|node.asType(PathNode)}
	end

	property readonly urnSpec: URNspec
	getter is do
		result:=urndefinition.urnspec
	end
	
	 
}