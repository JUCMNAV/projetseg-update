package aoUrnToRam;

require kermeta
require "platform:/resource/aoUrnToRam/src/aoUrnToIw/_Ref.kmt"
require "platform:/resource/aoUrnToRam/src/iwToIw/InsertInputProcessingNodes/_Ref.kmt"
require "platform:/resource/aoUrnToRam/src/iwToIw/LinkSteps/_Ref.kmt"
require "platform:/resource/aoUrnToRam/src/iwToRam/_Ref.kmt"
require "platform:/resource/aoUrnToRam/src/iwToStepView/_Ref.kmt"
require "platform:/resource/aoUrnToRam/src/ModelRepository.kmt"

using kermeta::persistence
using kermeta::standard
using kermeta::io
using oneurn::urn
using intermediateWorkflow
using ramstructural
using aoUrnToRam::util

class AoUrnToRamTransformation
{
	attribute repository:ModelRepository
	attribute dotAbsoluteFileUri:String
	attribute iwFileName:String
	attribute isIwFileOutputEnabled:Boolean
	attribute isRamStepViewAsDotOutputEnabled:Boolean
	attribute imgFolderAbsoluteFileUri:String
	
	operation initialize(dotAbsoluteFileUri:String,imgFolderAbsoluteFileUri:String):AoUrnToRamTransformation is do
		self.dotAbsoluteFileUri:=dotAbsoluteFileUri
		self.imgFolderAbsoluteFileUri:=imgFolderAbsoluteFileUri
		
		repository:= ModelRepository.new
		iwFileName:="Iw.xmi"
		isIwFileOutputEnabled:=false
		isRamStepViewAsDotOutputEnabled:=false
		result:=self 
	end

	operation transform(sourceAbsoluteFileUri:String,destinationAbsoluteFileUri:String) is do
		//stle: useful for development but remove on release code
		registerMetamodels
		//stle: until destination merge is implemented
		extern aoUrnToRam::javaExternalCall::File.removeDirectory_DirectoryUri(destinationAbsoluteFileUri)
		
		var urnSpec:URNspec urnSpec?=repository.load(sourceAbsoluteFileUri)
		var iwModel:IwModel init aoUrnToIw(urnSpec)
		
		outputIwFile(iwModel,destinationAbsoluteFileUri)
		
		outputRamStepClassDiagram(iwModel,destinationAbsoluteFileUri)
		
		var stepViews:OrderedSet<StepView> init iwModel.toStepView(absoluteFileUriToWindowsPath(imgFolderAbsoluteFileUri))
		outputRamStepViewAsDot(stepViews,destinationAbsoluteFileUri)
		outputRamStepViewAsImg(stepViews,destinationAbsoluteFileUri)
	end
	
	operation outputIwFile(iwModel:IwModel,destinationAbsoluteFileUri:String) is do
		if(isIwFileOutputEnabled) then
			repository.save(iwModel,destinationAbsoluteFileUri+"\\"+iwFileName,"platform:/resource/aoUrnToRam/metamodel/intermediateWorkflow.ecore")
		end
	end
	
	operation outputRamStepClassDiagram(iwModel:IwModel,destinationAbsoluteFileUri:String) is do
		iwModel.toRam.each{ramStepClassDiagram|
			var ramStepClassDiagramAbsoluteFileUri:String init Path.new.combine(destinationAbsoluteFileUri,ramStepClassDiagram.workspacePath)+".xmi"
			repository.save(ramStepClassDiagram,ramStepClassDiagramAbsoluteFileUri,"platform:/resource/aoUrnToRam/metamodel/RAMStructural.ecore")
		}
	end

	operation outputRamStepViewAsDot(stepViews:OrderedSet<StepView>,destinationAbsoluteFileUri:String) is do
		if(isRamStepViewAsDotOutputEnabled) then
			stepViews.each{stepView|
				var stepViewAsDotAbsoluteFileUri:String init Path.new.combine(destinationAbsoluteFileUri,stepView.workspacePath)+".dot" 
				var fileIo:FileIO init FileIO.new
				fileIo.writeTextFile(stepViewAsDotAbsoluteFileUri,stepView.dot)
			} 
	    end
	end

	operation outputRamStepViewAsImg(stepViews:OrderedSet<StepView>,destinationAbsoluteFileUri:String) is do
		stepViews.each{stepView|
			var stepViewAsImgAbsoluteFileUri:String init Path.new.combine(destinationAbsoluteFileUri,stepView.workspacePath)+".png"
			var stepViewAsImgFilePath:String init absoluteFileUriToWindowsPath(stepViewAsImgAbsoluteFileUri)
			var dotFilePath:String init absoluteFileUriToWindowsPath(dotAbsoluteFileUri)
			
		    extern aoUrnToRam::javaExternalCall::CommandLine.exe(dotFilePath+" -Tpng -o\""+stepViewAsImgFilePath+"\"",stepView.dot)
	    }
	end
	
	operation aoUrnToIw(urnSpec:URNspec):IwModel is do
		result:=urnSpec.toIw
		result.insertInputProcessingNodes
		result.linkSteps
	end

	//Only Windows is supported at this point
	operation absoluteFileUriToWindowsPath(absoluteFileUri:String):String is do
		result:=absoluteFileUri.replace("file:///","")
		result:=result.replace("/","\\")
	end
	
	operation registerMetamodels() is do
		var repository : EMFRepository init EMFRepository.new
		repository.registerEcoreFile("platform:/resource/aoUrnToRam/metamodel/oneurn.ecore")
		repository.registerEcoreFile("platform:/resource/aoUrnToRam/metamodel/intermediateWorkflow.ecore")
	end
}