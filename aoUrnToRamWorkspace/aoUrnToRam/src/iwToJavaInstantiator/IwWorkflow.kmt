package intermediateWorkflow;

require "platform:/resource/aoUrnToRam/src/iwToJavaInstantiator/_Ref.kmt"

using kermeta::standard
using javaInstantiator
using aoUrnToRam::util //stle:think about this

aspect class IwWorkflow {
	property readonly jiSUBPACKAGE:String 
	getter is do
		//STLE:DRY with other trans
		result:="Instantiators"
	end
	
	property readonly jiWORKFLOW_INSTANTIATOR_BASE_CLASS:String 
	getter is do
		result:="WorkflowInstantiator"
	end

	property readonly jiCustomizedClassSubPackage:String 
	getter is do
		result:=model.name+".Steps."+concern.name
	end
	
	property readonly jiWorkspacePath:String 
	getter is do
		result:=Path.new.combine(jiSUBPACKAGE,Path.new.combine(concern.name,name+jiWORKFLOW_INSTANTIATOR_BASE_CLASS))
	end

	operation jiToWorkflowInstantiator():WorkflowInstantiator is do
		result:=WorkflowInstantiator.new.initialize_WorkflowInstantiator(jiWorkspacePath,jiCustomizedClassSubPackage)
		jiAppendContent(result)
	end
	
	operation jiAppendContent(workflowInstantiator:WorkflowInstantiator) is do
		jiAppendPackage(workflowInstantiator)
		jiAppendImports(workflowInstantiator)
		jiAppendClassPre(workflowInstantiator)
		jiAppendBuildStatements(workflowInstantiator)
		jiAppendClassPost(workflowInstantiator)
	end
	
	operation jiAppendPackage(workflowInstantiator:WorkflowInstantiator) is do
		workflowInstantiator.appendPackage(model.name+"."+jiSUBPACKAGE+"."+concern.name)
	end
	
	operation jiAppendImports(workflowInstantiator:WorkflowInstantiator) is do
		workflowInstantiator.appendImport("ram.reactiveworkflow.*")
		workflowInstantiator.appendImport("ram.workflow.*")
		workflowInstantiator.append("\n")
	end
	
	operation jiAppendClassPre(workflowInstantiator:WorkflowInstantiator) is do
		workflowInstantiator.append("public class ")
		workflowInstantiator.append(name)
		workflowInstantiator.append("WorkflowInstantiator extends WorkflowInstantiator{\n")
	end

	operation jiAppendBuildStatements(workflowInstantiator:WorkflowInstantiator) is do
		nodes.each{node|node.jiAppendBuildStatement(workflowInstantiator)}
	end
	
	operation jiAppendClassPost(workflowInstantiator:WorkflowInstantiator) is do
		workflowInstantiator.append("}\n")	
	end
}