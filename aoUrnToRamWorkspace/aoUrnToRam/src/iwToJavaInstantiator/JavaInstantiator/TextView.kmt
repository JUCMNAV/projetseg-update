package aoUrnToRam;

require kermeta

using kermeta::standard
using kermeta::utils

//stle:dry with StepView
class TextView{
	attribute workspacePath: String
	attribute _content:StringBuffer
	
	operation initialize(workspacePath:String):TextView is do
		self.workspacePath:=workspacePath
		self._content:=StringBuffer.new
		result:=self
	end
	
	property readonly content : String
	getter is do
		result:=_content.toString
	end
	
	operation append(toAppend:String) is do
		_content.append(toAppend)
	end
	
	operation appendLine(line:String) is do
		append(line)
		appendNextLine 
	end
	
	operation appendNextLine() is do
		append("\n")
	end
	
	operation appendIndent(level:Integer,toAppend:String) is do
		level.times{i|append("    ")}
		append(toAppend)
	end
	
	operation appendWithDelimiter<T>(col:Collection<T>,delimiter:String,parse:<T->String>) is do
		var isFirst:Boolean init true
		col.each{e|
			if(isFirst) then
				isFirst:=false
			else
				append(delimiter)
			end
			append(parse(e))
		}
	end
}