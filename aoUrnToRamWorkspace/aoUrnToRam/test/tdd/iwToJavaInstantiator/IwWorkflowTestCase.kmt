package aoUrnToRam::test;

require "platform:/resource/aoUrnToRam/test/tdd/iwToJavaInstantiator/TestHelper.kmt"

using kermeta::standard
using kermeta::kunit
using intermediateWorkflow
using aoUrnToRam::util 
using javaInstantiator

class IwToJavaInstantiator_IwWorkflowTestCase inherits TestCase,IwToJavaInstantiator_TestHelper
{
	reference sut:IwWorkflow
	reference workflowInstantiator:WorkflowInstantiator
	
	method setUp() is do
		sut:=createIwWorkflowWithContainers
	end

	@feature "FeaWorkflowInstantiationWithoutStubs"
	operation testToWorkflowInstantiator_ShouldInitWorkspacePath() is do
		workflowInstantiator:=sut.jiToWorkflowInstantiator
		
		assertEquals("Instantiators/theConcern/theWorkflowWorkflowInstantiator",workflowInstantiator.workspacePath)
	end

	@feature "FeaWorkflowInstantiationWithoutStubs"
	operation testToWorkflowInstantiator_ShouldInitCustomizableClassSubPackage() is do
		workflowInstantiator:=sut.jiToWorkflowInstantiator
		
		assertEquals("theModel.Steps.theConcern",workflowInstantiator.customizableClassSubPackage)
	end
	

	@feature "FeaWorkflowInstantiationWithoutStubs"
	operation testToWorkflowInstantiator_ShouldAppendPackage() is do
		workflowInstantiator:=sut.jiToWorkflowInstantiator
		
		assertStringStartsWith("package theModel.Instantiators.theConcern;\n\n",workflowInstantiator.content)
	end

	@feature "FeaWorkflowInstantiationWithoutStubs"
	operation testToWorkflowInstantiator_ShouldAppendImports() is do
		workflowInstantiator:=sut.jiToWorkflowInstantiator
		
		assertStringContains( "import ram.reactiveworkflow.*;\n",workflowInstantiator.content)
		assertStringContains( "import ram.workflow.*;\n",workflowInstantiator.content)
	end

	@feature "FeaWorkflowInstantiationWithoutStubs"
	operation testToWorkflowInstantiator_ShouldAppendClass() is do
		workflowInstantiator:=sut.jiToWorkflowInstantiator
		
		assertStringContains("\n\npublic class theWorkflowWorkflowInstantiator extends WorkflowInstantiator{",workflowInstantiator.content)
		assertStringEndsWith("}\n",workflowInstantiator.content)
	end

	@feature "FeaWorkflowInstantiationWithoutStubs"
	operation testToWorkflowInstantiator_ShouldAppendLinkMethod() is do
		workflowInstantiator:=sut.jiToWorkflowInstantiator
		
		assertStringContains("    public void link(){\n    }\n"
							,workflowInstantiator.content)
	end

	@feature "FeaWorkflowInstantiationWithoutStubs"
	operation testToWorkflowInstantiator_ShouldInvokeAppendLinkStatementOnNodeConnections() is do
		var step:IwStep init createIwStep
		var first:IwNode init createIwCustomizableNode_Workflow(sut)
		first.step:=step
		var inBetween:IwNode init createIwOutput_Workflow(sut)
		inBetween.step:=step
		var last:IwNode init createIwEndPoint_Workflow(sut)
		last.step:=step
		
		linkNodes(first,inBetween)
		linkNodes(inBetween,last)
		
		workflowInstantiator:=sut.jiToWorkflowInstantiator
		
		assertStringContainsExpectedNTime(".addNextNode",2,workflowInstantiator.content)
	end
}
