package aoUrnToRam::test;

require "platform:/resource/aoUrnToRam/test/tdd/iwToJavaInstantiator/TestHelper.kmt"

using kermeta::standard
using kermeta::kunit
using intermediateWorkflow
using javaInstantiator

class IwToJavaInstantiator_IwNodeConnectionTestCase inherits TestCase,IwToJavaInstantiator_TestHelper
{
	reference sut:IwNodeConnection
	reference workflowInstantiator:WorkflowInstantiator
	
	method setUp() is do
		workflowInstantiator:=createWorkflowInstantiator
	end
	

	operation testAppendBuildStatement_ShouldDeclareAndInitializeWorkflowNode() is do
		var orFork:IwOrFork init createIwOrFork
		orFork.step:=createIwStep
		sut:=withCondition(connectSource(orFork))
		
		sut.jiAppendBuildStatement(workflowInstantiator)
		
		assertEquals("    private Condition _theOrFork_theCondition=createCondition(\"theCustomizableClassSubPackage.theStep.theOrFork_theCondition\");\n"
					,workflowInstantiator.content)
	end
	
	operation testAppendLinkStatement_WithoutCondition_ShouldLinkSourceAndTarget() is do
		sut:=linkNodes(createIwCustomizableNode,createIwEndPoint)
		
		sut.jiAppendLinkStatement(workflowInstantiator)
		
		assertEquals("        _theCustomizableNode.addNextNode(_theEndPoint);\n"
					,workflowInstantiator.content)
	end
	
	operation testAppendLinkStatement_WithCondition_ShouldLinkSourceAndTarget() is do
		sut:=withCondition(linkNodes(createIwOrFork,createIwEndPoint))
		
		sut.jiAppendLinkStatement(workflowInstantiator)
		
		assertEquals("        _theOrFork.addNextNode(\"theCondition\",_theEndPoint,_theOrFork_theCondition);\n"
					,workflowInstantiator.content)
	end
	
	
}