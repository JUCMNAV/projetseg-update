package aoUrnToRam::test;
require "platform:/resource/aoUrnToRam/test/tdd/aoUrnToIw/TestHelper.kmt"

using kermeta::standard
using kermeta::kunit
using oneurn::urncore
using oneurn::ucm::map
using intermediateWorkflow

class AoUrnToIw_PathNode_LinkTestCase inherits TestCase,AoUrnToIw_TestHelper {
	reference sut:PathNode
	
	@feature "FeaTransformEmptyPointResponsibilityEndPoint"
	operation testLink_OutIn_ShouldLinkThreeIwNodesToIwWorklow() is do
		sut:=createStartPoint
		setupAndExeLink
		
		assertCollectionHasThreeElements(sut.iwEquivalentNode,sut.iwInput,sut.iwOutput,sut.ucmMap.iwWorkflow.nodes)
	end
	
	@feature "FeaTransformEmptyPointResponsibilityEndPoint"
	operation testLink_OutIn_ShouldLinkIwOutputToIwInput() is do
		sut:=createStartPoint
		setupAndExeLink
		
		assertCollectionHasSingleElement(sut.iwInput,sut.iwOutput.targets)
	end

	@feature "FeaTransformEmptyPointResponsibilityEndPoint"
	operation testLink_OutputInputAfter_ShouldLinkIwEquivalentNodeToIwOutput() is do
		sut:=createStartPoint//use StartPoint (OutputInputAfter)
		setupAndExeLink
		
		assertCollectionHasSingleElement(sut.iwOutput,sut.iwEquivalentNode.targets)
	end
	
	@feature "FeaTransformEmptyPointResponsibilityEndPoint"
	operation testLink_OutputInputBefore_ShouldLinkIwInputToIwEquivalentNode() is do
		sut:=createEndPoint//use EndPoint (OutputInputBefore)
		setupAndExeLink
		
		assertCollectionHasSingleElement(sut.iwEquivalentNode,sut.iwInput.targets)
	end
	
	@feature "FeaTransformEmptyPointResponsibilityEndPoint"
	operation testLink_ShouldLinkIwWorkflow() is do
		var ucmMap:UCMmap init createUcmMap
		sut:=createEndPoint_UcmMap(ucmMap)//use EndPoint as sut impl
		ucmMap.build
		
		sut.link
		
		assertCollectionHasSingleElement(sut.iwEquivalentNode,ucmMap.iwWorkflow.nodes)
	end
	
	@feature "FeaTransformOrFork"
	operation testLink_NotEmpty_ShouldInvokeLinkOnNodeConnections() is do
		var ucmMap:UCMmap init createUcmMap
		sut:=createOrFork_UcmMap(ucmMap)
		connectEndPointAsTarget(sut)
		connectEndPointAsTarget(sut)
		ucmMap.build

		sut.link

		assertEquals(2,sut.iwEquivalentNode.succs.size)
	end
	
	@feature "FeaTransformOrFork"
	operation testLink_Empty_ShouldNotInvokeLinkOnNodeConnections() is do
		var ucmMap:UCMmap init createUcmMap
		sut:=createEmptyPoint_UcmMap(ucmMap)
		var nodeConnection:NodeConnection init connectEndPointAsTarget(sut)  
		ucmMap.build

		sut.link

		assertVoid(nodeConnection.iwNodeConnection)
	end
	
	operation setupAndExeLink() is do
		var ucmMap:UCMmap init createUcmMap
		sut.ucmMap:=ucmMap
		attachOutputInputRamMetadata(sut)
		ucmMap.build
		
		sut.link
	end
}