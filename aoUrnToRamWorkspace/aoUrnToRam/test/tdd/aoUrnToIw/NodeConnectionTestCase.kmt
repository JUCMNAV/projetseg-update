package aoUrnToRam::test;
require "platform:/resource/aoUrnToRam/test/tdd/aoUrnToIw/TestHelper.kmt"

using kermeta::standard
using kermeta::kunit
using oneurn::urncore
using oneurn::ucm::map
using intermediateWorkflow

class AoUrnToIw_NodeConnectionTestCase inherits TestCase,AoUrnToIw_TestHelper {
	reference sut:NodeConnection
	
	method setUp() is do
		sut:=createNodeConnection
	end
	
	//stle: add not void test if not covered by invoke link test
	
	@feature "FeaTransformOrFork"
	operation testBuild_WithCondition_ShouldSetConditionName() is do
		withCondition(sut)

		sut.build
	
		assertEquals(sut.conditionLabel,sut.iwNodeConnection.conditionName)
	end	

	@feature "FeaTransformStartPointResponsibilityEndPoint"
	operation testLink_SourceInOut_ShouldLinkInputAsSource() is do
		var ucmMap:UCMmap init createUcmMap
		sut.source:=createStartPoint_UcmMap(ucmMap)
		sut.target:=createEndPoint_UcmMap(ucmMap)
		attachOutputInputRamMetadata(sut.sourceAsPathNode)
		ucmMap.build
	
		sut.link
	
		assertEquals(sut.sourceAsPathNode.iwInput,sut.iwNodeConnection.source)
	end
	
	@feature "FeaTransformStartPointResponsibilityEndPoint"
	operation testLink_TargetInOut_ShouldLinkOutputAsTarget() is do
		var ucmMap:UCMmap init createUcmMap
		sut.source:=createStartPoint_UcmMap(ucmMap)
		sut.target:=createEndPoint_UcmMap(ucmMap)
		attachOutputInputRamMetadata(sut.targetAsPathNode)
		ucmMap.build
	
		sut.link
	
		assertEquals(sut.targetAsPathNode.iwOutput,sut.iwNodeConnection.target)
	end	

	@feature "FeaTransformStartPointResponsibilityEndPoint"
	operation testLink_EmptyNodeInBetween_ShouldLinkTarget() is do
		var ucmMap:UCMmap init createUcmMap
		sut.source:=createStartPoint_UcmMap(ucmMap)
		 //EmptyPoint as PathNodeImpl without IwNodes
		var emptyNode:PathNode init createEmptyPoint_UcmMap(ucmMap)
		sut.target:=emptyNode
		var target:EndPoint init createEndPoint_UcmMap(ucmMap)
		connectNodes(emptyNode,target) 
		ucmMap.build
	
		sut.link
		
		assertEquals(target.iwEndPoint,sut.iwNodeConnection.target)
	end
	
	
		
}