package aoUrnToRam::Test;

require kermeta
require "platform:/resource/aoUrnToRam/test/tdd/iwToIw/LinkSteps/TestHelper.kmt"

using kermeta::standard
using kermeta::kunit
using intermediateWorkflow

class LinkSteps_IwStepTestCase inherits TestCase,LinkSteps_TestHelper
{
	reference concern:IwConcern
	reference workflow:IwWorkflow
	reference toMerge:IwStep
	reference sut:IwStep
	
	method setUp() is do
		//stle:dry
		concern:= createIwConcern
    	workflow:=createIwWorkflow
    	workflow.concern:=concern

		toMerge:=createIwStep
		toMerge.name:="a"
		toMerge.concern:=concern
		sut:=createIwStep
		sut.name:="z"
		sut.concern:=concern
	end
	
	@feature "FeaNamingOfRAMStep"
	operation testMerge_StepNameConcatenatedInAlphabeticalOrder() is do
		sut.merge(toMerge)
		
		assertEquals("a_z",sut.name)
	end
	
	operation testMerge_StartupStep_FF() is do
		parameterizableTestMerge_StartupStep(false,"a_z",false,false)
	end
	
	operation testMerge_StartupStep_FT() is do
		parameterizableTestMerge_StartupStep(false,"z",false,true)
	end

	operation testMerge_StartupStep_TF() is do
		parameterizableTestMerge_StartupStep(false,"a",true,false)
	end

	operation testMerge_StartupStep_TT() is do
		parameterizableTestMerge_StartupStep(true,"a_z",true,true)
	end
	
	operation parameterizableTestMerge_StartupStep(expectedIsStartupStep:Boolean,expectedName:String,sutIsStartupStep:Boolean,toMergeIsStartupStep:Boolean) is do
		sut.isStartupStep:=sutIsStartupStep
		toMerge.isStartupStep:=toMergeIsStartupStep
	
		sut.merge(toMerge)
		
		assertEquals(expectedIsStartupStep,sut.isStartupStep)
		assertEquals(expectedName,sut.name)
	end
	
	
	
	@feature "FeaNamingOfRAMStep"
	operation testMerge_toMergeDeleted() is do
		linkTwoNodes(toMerge)
		toMerge.outboundStubs.add(createIwStub)
		toMerge.outboundStubs.add(createIwStub)
	
		sut.merge(toMerge)
		
		assertVoid(toMerge.concern)
		assertEquals(0,toMerge.nodes.size)
		assertEquals(0,toMerge.outboundStubs.size)
	end
	
	@feature "FeaNamingOfRAMStep"
	operation testMerge_NodeAdded() is do
		linkTwoNodes(sut)
		linkTwoNodes(toMerge)
		
		sut.merge(toMerge)
		
		assertEquals(4,sut.nodes.size)
	end
	
	operation linkTwoNodes(step:IwStep) is do
		step.nodes.add(createIwCustomizableNode)
		step.nodes.add(createIwCustomizableNode)
	end
}