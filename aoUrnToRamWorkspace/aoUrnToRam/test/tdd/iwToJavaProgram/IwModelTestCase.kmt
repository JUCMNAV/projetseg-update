package aoUrnToRam::test;

require "platform:/resource/aoUrnToRam/test/tdd/iwToJavaProgram/TestHelper.kmt"

using kermeta::standard
using kermeta::kunit
using intermediateWorkflow
using aoUrnToRam

class IwToJavaProgram_IwModelTestCase inherits TestCase,IwToJavaProgram_TestHelper
{
	reference sut:IwModel
	reference javaView:JavaView
	
	method setUp() is do
		sut:=createIwModel
	end
	
	operation testToJavaView_ShouldInitWorkspacePath() is do
		javaView:=sut.jpToJavaProgram
		
		assertEquals("Program",javaView.workspacePath)
	end

	operation testToJavaView_ShouldAppendPackage() is do
		javaView:=sut.jpToJavaProgram
		
		assertStringStartsWith("package theModel;\n\n",javaView.content)
	end
	
	operation testToWorkflowInstantiator_ShouldAppendImportReactiveWorkflowSystem() is do
		javaView:=sut.jpToJavaProgram
		
		assertStringContains("import ram.reactiveworkflow.ReactiveWorkflowSystem;\n",javaView.content)
	end
	
	operation testToJavaView_TwoWorkflowsDifferentConcerns_ShouldInvokeAppendImportOnWorkflows() is do
		setupTwoWorkflowsDifferentConcerns
	
		javaView:=sut.jpToJavaProgram
		
		assertStringContainsNTime(2,"import theModel.Instantiators.",javaView.content)
	end
	
	operation testToJavaView_ShouldAppendClass() is do
		javaView:=sut.jpToJavaProgram
		
		assertStringContains("\n\npublic class Program{\n",javaView.content)
		assertStringEndsWith("}\n",javaView.content)
	end

	operation testToJavaView_ShouldAppendMainMethodSkeleton() is do
		javaView:=sut.jpToJavaProgram
		
		assertStringContains(
"
    public static void main(String[] args){
        ReactiveWorkflowSystem reactiveWorkflowSystem=new ReactiveWorkflowSystem();


        reactiveWorkflowSystem.start();
    }
"
		,javaView.content)		
	end
	
	operation testToJavaView_TwoWorkflowsDifferentConcerns_ShouldInvokeAppendBuildOnWorkflows() is do
		setupTwoWorkflowsDifferentConcerns
	
		javaView:=sut.jpToJavaProgram
		
		assertStringContainsNTime(2,"new theWorkflowWorkflowInstantiator",javaView.content)
	end
	
	operation testToJavaView_TwoWorkflowsDifferentConcerns_ShouldInvokeAppendLinkOnWorkflows() is do
		setupTwoWorkflowsDifferentConcerns
	
		javaView:=sut.jpToJavaProgram
		
		assertStringContainsNTime(2,".link()",javaView.content)
	end

	operation testToJavaView_TwoWorkflowsDifferentConcerns_ShouldInvokeAppendAddWorkflow() is do
		setupTwoWorkflowsDifferentConcerns
	
		javaView:=sut.jpToJavaProgram
		
		assertStringContainsNTime(2,".addWorkflow()",javaView.content)
	end
	
	operation setupTwoWorkflowsDifferentConcerns() is do
		createIwWorkflow.withConcern(createIwConcern.withModel(sut))
		createIwWorkflow.withConcern(createIwConcern.withModel(sut))
	end
}
