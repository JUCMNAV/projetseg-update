package aoUrnToRam::Test;

require kermeta
require "platform:/resource/aoUrnToRam/test/tdd/iwToRam/IwTestFactory.kmt"
require "platform:/resource/aoUrnToRam/src/iwToRam/Build.kmt"

using kermeta::standard
using kermeta::kunit
using intermediateWorkflow
using ramstructural


class IwToRamBuildTestCase inherits TestCase
{
	attribute factory:IwTestFactory

	method setUp() is do
		factory:=IwTestFactory.new
	end
	
	@feature "FeaTransformStartPointResponsibilityEndPoint"
	operation testBuildInput() is do
		var actual:IwInput init factory.createIwInput()
		actual.build()
		
		assert(actual.ramClasses_PerSourceModelElement.size==1)
		assertClassProperlyBuilt(actual.ramInputData,"a"+actual.INPUT_SUFFIX)
	end

	@feature "FeaTransformStartPointResponsibilityEndPoint"
	operation testBuildOut() is do
		var actual:IwOutput init factory.createIwOutput()
		actual.build()
		
		assert(actual.ramClasses_PerSourceModelElement.size==2)
		assertClassProperlyBuilt(actual.ramOutputData,"a"+actual.OUTPUT_SUFFIX)
		assertClassProperlyBuilt(actual.ramCustomizableNode,"a")
	end

	@feature "FeaTransformStartPointResponsibilityEndPoint"
	operation testBuildProcessingNode() is do
		var actual:IwProcessingNode init factory.createIwProcessingNode()
		actual.build()

		assert(actual.ramClasses_PerSourceModelElement.size==1)
		assertClassProperlyBuilt(actual.ramCustomizableNode,"a")
	end
	

	@feature "FeaTransformStartPointResponsibilityEndPoint"
	operation testBuildWorkflow() is do
		var actual:IwWorkflow init factory.createIwWorkflow()
		actual.name:="a"
		actual.build()
		
		assertIsNonVoidInstanceOf(Aspect,actual.ramAspect)
		//stle: aspect is name of workflow for now, wait until steps get implemented
		assertStringEqual("a",actual.ramAspect.name)
		assertIsNonVoidInstanceOf(ClassDiagram,actual.ramAspect.structuralView)
		assertIsNonVoidInstanceOf(AspectInterface,actual.ramAspect.interface)
		
		assertIsNonVoidInstanceOf(Instantiation,actual.ramReactiveWorkflowInstantiation)
		assertStringEqual("ReactiveWorkflow",actual.ramReactiveWorkflowInstantiation.externalAspect)
	end

	@feature "FeaTransformOrFork"
	operation testBuildIwNodeConnection_WithConditions() is do
		var source:IwOrFork init factory.createIwOrFork
		source.name:="theOrFork"
		var actual:IwNodeConnection init factory.createIwNodeConnection(source,factory.createIwProcessingNode,"theBranch")
		actual.build()

		assertClassProperlyBuilt(actual.ramCondition,"theOrFork_theBranch")
	end

	@feature "FeaTransformOrFork"
	operation testBuildIwNodeConnection_WithoutConditions() is do
		var source:IwProcessingNode init factory.createIwProcessingNode
		var actual:IwNodeConnection init factory.createIwNodeConnection(source,factory.createIwProcessingNode,void)
	
		actual.build()
	
		assert(actual.ramCondition.isVoid)
	end
	
	operation assertClassProperlyBuilt(ramClass:Class,name:String) is do
		assertIsNonVoidInstanceOf(Class,ramClass)
		assertStringEqual(name,ramClass.name)
	end
	
	
	//stle: dry
	//stle: more verbrose on assertion
	operation assertIsNonVoidInstanceOf(expected:kermeta::language::structure::Class, actual:Object) is do
		assertWithMsg(actual.isVoid==false,"IsVoid-> expected:false actual:true")
		assertWithMsg(actual.isInstanceOf(expected),"IsInstanceOf-> expected:\""+expected.toString()+"\"actual:\""+actual.getMetaClass().toString()+"\"")
	end

	
	operation assertStringEqual(expected:String, actual:String) is do
		assertWithMsg(expected==actual,"StringEqual-> expected:\""+expected+"\"actual:\""+actual+"\"")
	end
}