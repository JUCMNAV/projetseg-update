package intermediateWorkflow;

require kermeta
require "platform:/resource/aoUrnToRam/src/iwToRam/Structural.kmt"

using kermeta::standard
using kermeta::utils
using ramstructural

aspect class IwModel {
	operation build() is do
		concerns.each{concern|concern.build}
	end
}

aspect class IwConcern {
	operation build() is do
		steps.each{step|
			step.build()
			step.buildConcern(self)
		}
	end
}

aspect class IwStep {
	operation buildConcern(concern:IwConcern) is do
		ramAspect.workspaceDirectory:=concern.name
	end

	operation build() is do
		ramAspect:=Aspect.new
		ramAspect.name:=self.name
		ramAspect.structuralView:=ClassDiagram.new
		ramAspect.interface:=AspectInterface.new
		
		ramReactiveWorkflowInstantiation:=Instantiation.new
		ramReactiveWorkflowInstantiation.externalAspect:="ReactiveWorkflow"
		
		nodes.each{node|node.build()}
	end
}

aspect class IwNode {
	operation build() is do
		ramClasses_PerSourceModelElement:=Hashtable<String, Class>.new
	end
	
	operation addClass(sourceModelElement:String, name:String) is do
		var ramClass:Class init Class.new
		ramClass.name:=name
		ramClasses_PerSourceModelElement.put(sourceModelElement,ramClass)
	end
}

aspect class IwInput {
	method build() is do
		super()
		addClass(INPUT_DATA,self.name+INPUT_SUFFIX)
	end
}

aspect class IwOutput {
	method build() is do
		super()
		addClass(OUTPUT_DATA,self.name+OUTPUT_SUFFIX)
		addClass(CUSTOMIZABLE_NODE,self.name)
	end
}

aspect class IwCustomizableNode {
	method build() is do
		super()
		addClass(CUSTOMIZABLE_NODE,self.name)
	end
}