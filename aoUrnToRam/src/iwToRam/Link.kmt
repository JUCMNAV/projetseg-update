package intermediateWorkflow;

require kermeta
require "platform:/resource/aoUrnToRam/src/iwToRam/Structural.kmt"

using kermeta::standard
using kermeta::utils
using ramstructural

aspect class IwModel {
	operation link() is do
		concerns.each{concern|concern.link}
		addAspects
	end
	
	operation addAspects() is do
		concerns.each{concern|
			concern.steps.select{step|step.ramAspect.structuralView.classes.size>0}
				 			 .each{step|ramAspects.add(step.ramAspect)}
		}
	end
}

aspect class IwConcern {
	operation link() is do
		steps.each{step|step.link}
	end
}

aspect class IwStep {
	operation link() is do
		addClasses
		addInstantiations
		nodes.each{node|node.link}
	end

	operation addClasses() is do
		nodes.each{node|
			node.ramClasses_PerSourceModelElement.values.each{ramClass|
				ramStructuralView.classes.add(ramClass)
			}
		}
	end
	
	operation addInstantiations() is do
		ramStructuralView.instantiations.add(ramReactiveWorkflowInstantiation)
		addReactiveWorkflowMappings
	end
	
	operation addReactiveWorkflowMappings() is do
		var mappings: Hashtable<String, Mapping> init Hashtable<String, Mapping>.new 
		
		nodes.each{node|
			node.ramClasses_PerSourceModelElement.keys.each{sourceModelElement|
				var ramClass:Class init node.ramClasses_PerSourceModelElement.getValue(sourceModelElement) 
				getMapping(mappings,sourceModelElement).maps.add(ramClass)
			}
		}
	end
	
	operation getMapping(mappings: Hashtable<String, Mapping>,sourceModelElement:String):Mapping is do
		if(mappings.containsKey(sourceModelElement)==false) then
			var mapping:Mapping init Mapping.new
			mapping.sourceModelElement:=sourceModelElement
			mappings.put(sourceModelElement,mapping)
			ramReactiveWorkflowInstantiation.mappings.add(mapping)
		end
		result:=mappings.getValue(sourceModelElement)
	end
	
	operation addRamClass(ramClass:Class) is do
		ramStructuralView.classes.add(ramClass)
	end
	
	operation mapRamClass(sourceModelElement:String,ramClass:Class) is do
		//stle:single again
		var mapping:Mapping init ramReactiveWorkflowInstantiation.mappings.select
								{mapping|mapping.sourceModelElement==sourceModelElement}.one  
		if(mapping.isVoid) then
			mapping:=Mapping.new
			mapping.sourceModelElement:=sourceModelElement
			ramReactiveWorkflowInstantiation.mappings.add(mapping)
		end
		mapping.maps.add(ramClass)
	end
}

aspect class IwNode {
	operation link() is do
		//stle: empty default for now, abstract later
	end
}

aspect class IwOrFork {
	method link() is do
		succs.each{succ|
			var ramCondition:Class init Class.new
			ramCondition.name:=name+"_"+succ.conditionName
			step.addRamClass(ramCondition)
			step.mapRamClass("Condition",ramCondition)
		}
	end
}