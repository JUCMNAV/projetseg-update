package intermediateWorkflow;

require kermeta
require "platform:/resource/aoUrnToRam/src/iwToRam/Structural.kmt"

using kermeta::standard
using kermeta::utils
using ramstructural

aspect class IwModel {
	operation link() is do
		workflows.each{workflow|
			workflow.link
		}
		
		addAspects
	end
	
	operation addAspects() is do
		ramAspects.addAll(
			workflows.collect{workflow|workflow.ramAspect}
		)
	end
}

aspect class IwWorkflow {
	operation link() is do
		addClasses
		addInstantiations
	end
	
	operation addClasses() is do
		nodes.each{node|
			node.ramClasses_PerSourceModelElement.values.each{ramClass|
				ramStructuralView.classes.add(ramClass)
			}
		}
	end
	
	operation addInstantiations() is do
		ramStructuralView.instantiations.add(ramReactiveWorkflowInstantiation)
		addReactiveWorkflowMappings
	end
	
	operation addReactiveWorkflowMappings() is do
		var mappings: Hashtable<String, Mapping> init Hashtable<String, Mapping>.new 
		
		nodes.each{node|
			node.ramClasses_PerSourceModelElement.keys.each{sourceModelElement|
				createMappingOnFirstTime(mappings,sourceModelElement)
				var ramClass:Class init node.ramClasses_PerSourceModelElement.getValue(sourceModelElement) 
				mappings.getValue(sourceModelElement).maps.add(ramClass)
			}
		}
	end
	
	operation createMappingOnFirstTime(mappings: Hashtable<String, Mapping>,sourceModelElement:String) is do
		if(mappings.containsKey(sourceModelElement)==false) then
			var mapping:Mapping init Mapping.new
			mapping.sourceModelElement:=sourceModelElement
			mappings.put(sourceModelElement,mapping)
			ramReactiveWorkflowInstantiation.mappings.add(mapping)
		end
	end
	
}
