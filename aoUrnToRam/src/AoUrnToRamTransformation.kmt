package aoUrnToRam;

require kermeta
require "platform:/resource/aoUrnToRam/src/aoUrnToIw/Transformation.kmt"
require "platform:/resource/aoUrnToRam/src/iwToRam/Transformation.kmt"

using kermeta::persistence
using kermeta::standard
using oneurn::urn
using intermediateWorkflow
using ramstructural

class AoUrnToRamTransformation
{
	operation transform(source:String,destination:String, optional_intermediate:String) is do
		var urnSpec:URNspec init ModelRepository<URNspec>.new.load(source)
		var iwModel:IwModel init urnSpec.toIw
		if(optional_intermediate.isVoid==false)then
			ModelRepository<IwModel>.new.save(iwModel,optional_intermediate,"http:///intermediateWorkflow.ecore")
		end
		var ramAspect:Aspect init iwModel.toRam
		ModelRepository<Aspect>.new.save(ramAspect,destination,"platform:/resource/aoUrnToRam/metamodel/RAMStructural.ecore")
	end
}

//stle: try to use multiple inheritance. Then, package so, it can be reused
class ModelRepository<T>
{
     operation load(uri : String) : T is do
     	var repository : EMFRepository
     	repository := EMFRepository.new
    	var resource : Resource init repository.getResource(uri)
    	resource.load
    	result ?= resource.one
    end

    operation save(model: T, modelUri : String, metamodelUri: String) is do
    	var repository : EMFRepository
    	repository := EMFRepository.new
    	var resource : Resource init repository.createResource(modelUri,metamodelUri)
    	resource.add(model)
    	resource.save()
    end
}




