package aoUrnToRam;

require kermeta
require "platform:/resource/aoUrnToRam/src/aoUrnToIw/_Ref.kmt"
require "platform:/resource/aoUrnToRam/src/iwToIw/InsertInputProcessingNodes/_Ref.kmt"
require "platform:/resource/aoUrnToRam/src/iwToIw/LinkSteps/_Ref.kmt"
require "platform:/resource/aoUrnToRam/src/iwToRam/_Ref.kmt"
require "platform:/resource/aoUrnToRam/src/iwToStepView/_Ref.kmt"

using kermeta::persistence
using kermeta::standard
using kermeta::io
using oneurn::urn
using intermediateWorkflow
using ramstructural

class AoUrnToRamTransformation
{
	operation transform(source:String,destination:String, optional_intermediate:String) is do
		//stle: temp hardcoded
		var tempDestinationPath:String init "C:\\Users\\S\\Files\\A2011\\Project\\Source\\aoUrnToRam\\test\\integration\\model\\RamWorkspace\\"
		
		registerMetamodels
		
		extern aoUrnToRam::javaExternalCall::File.removeDirectory(tempDestinationPath)
		
		var repository:ModelRepository init ModelRepository.new
			
		var urnSpec:URNspec 
		urnSpec?=repository.load(source)
		
		var iwModel:IwModel init urnSpec.toIw
		
		iwModel.insertInputProcessingNodes
		iwModel.linkSteps
		
		if(optional_intermediate.isVoid==false)then
			repository.save(iwModel,optional_intermediate,"platform:/resource/aoUrnToRam/metamodel/intermediateWorkflow.ecore")
		end
		
		iwModel.toRam.each{ramAspect|
			repository.save(ramAspect,Path.new.combine(destination,ramAspect.workspacePath)+".xmi","platform:/resource/aoUrnToRam/metamodel/RAMStructural.ecore")
		}
		
		iwModel.toStepView.each{stepView|StepViewToPng(stepView,tempDestinationPath)}
	end

	operation StepViewToPng(stepView:StepView,destination:String) is do
		//stle: temp: hardcode paths
		var dotFilePath:String init "\"C:\\Users\\S\\Files\\A2011\\Project\\Source\\aoUrnToRam\\thridParty\\Graphviz2.26.3\\bin\\dot.exe\""
		var windowsWorkspacePath:String init stepView.workspacePath.replace("/","\\") 

//stle: remove that and provide a proper api	
//		stdio.writeln("---")
//		stdio.writeln(stepView.dot)
		 
	    extern aoUrnToRam::javaExternalCall::CommandLine.exe(dotFilePath+" -Tpng -o\""+destination+windowsWorkspacePath+".png\"",stepView.dot)
	end
	
	
	operation registerMetamodels() is do
		var repository : EMFRepository init EMFRepository.new
		repository.registerEcoreFile("platform:/resource/aoUrnToRam/metamodel/oneurn.ecore")
		repository.registerEcoreFile("platform:/resource/aoUrnToRam/metamodel/intermediateWorkflow.ecore")
	end
}

class ModelRepository
{
    operation load(uri : String) : Object is do
     	var repository : EMFRepository init EMFRepository.new 
    	var resource : Resource init repository.getResource(uri)
    	resource.load
    	result:=resource.one
    end

    operation save(model: Object, modelUri : String, metamodelUri: String) is do
    	fileDoesNotExistWorkAround(modelUri)
    
		var repository : EMFRepository init EMFRepository.new
    	var resource : Resource init repository.createResource(modelUri,metamodelUri)
    	resource.add(model)
    	resource.save()
    end
    
    operation fileDoesNotExistWorkAround(modelUri:String) is do
		FileIO.new.writeTextFile(modelUri, "")
    end
}




