package aoUrnToRam;

require kermeta
require "platform:/resource/aoUrnToRam/src/aoUrnToIw/Transformation.kmt"
require "platform:/resource/aoUrnToRam/src/iwToRam/Transformation.kmt"

using kermeta::persistence
using kermeta::standard
using kermeta::io
using oneurn::urn
using intermediateWorkflow
using ramstructural

class AoUrnToRamTransformation
{
	operation transform(source:String,destination:String, optional_intermediate:String) is do
		var repository:ModelRepository init ModelRepository.new
	
		var urnSpec:URNspec 
		urnSpec?=repository.load(source)
		
		var iwModel:IwModel init urnSpec.toIw
		if(optional_intermediate.isVoid==false)then
			repository.save(iwModel,optional_intermediate,"http:///intermediateWorkflow.ecore")
		end
		
		iwModel.toRam.each{ramAspect|
			repository.save(ramAspect,destination+ramAspect.name+".xmi","platform:/resource/aoUrnToRam/metamodel/RAMStructural.ecore")
		}
	end
}

class ModelRepository
{
     operation load(uri : String) : Object is do
     	var repository : EMFRepository init EMFRepository.new 
    	var resource : Resource init repository.getResource(uri)
    	resource.load
    	result:=resource.one
    end

//stle to avoid registering manually all the time    	
//        repository.registerEcoreFile("platform:/resource/fr.irisa.triskell.kermeta.samples/class2RDBMS/metamodels/RDBMSMM.ecore")

    operation save(model: Object, modelUri : String, metamodelUri: String) is do
    	fileDoesNotExistWorkAround(modelUri)
    
		var repository : EMFRepository init EMFRepository.new
    	var resource : Resource init repository.createResource(modelUri,metamodelUri)
    	resource.instances.add(model)
    	resource.save()
    end
    
    operation fileDoesNotExistWorkAround(modelUri:String) is do
		FileIO.new.writeTextFile(modelUri, "")
    end
}




