package intermediateWorkflow;

require kermeta
require "platform:/resource/aoUrnToRam/metamodel/intermediateWorkflow.ecore"

using kermeta::standard
using kermeta::utils

aspect class IwModel {
	operation linkSteps() is do
		concerns.each{concern|concern.linkSteps}
	end
}

aspect class IwConcern {
	operation linkSteps() is do
		workflows.each{workflow|workflow.linkSteps(self)}
	end
}

aspect class IwWorkflow {
	operation linkSteps(concern:IwConcern) is do
		startPoints.each{node|node.linkStep}
	end
	
	property readonly concern:IwConcern
	getter is do
		result?=container
	end
}

aspect class IwNode {
	operation step_DeepFirstSearch(currentStep:IwStep) is do
		if(isUnexplored) then
			explore(currentStep)
		else
			if(isPartOfStep(currentStep)==false) then
				currentStep.merge(step)
			end
		end
	end

	//virtual
	operation explore(currentStep:IwStep) is do
		step:=currentStep
		succs.each{nodeConnection|nodeConnection.target.step_DeepFirstSearch(step)}
	end
	
	operation isPartOfStep(currentStep:IwStep):Boolean is do
		result:=step==currentStep
	end
	
	property readonly isUnexplored : Boolean
	getter is do
		result:= step.isVoid
	end
	
	property readonly concern:IwConcern
	getter is do
		result:=workflow.concern
	end
}

aspect class IwStep {
	attribute _isPartiallyInitialized:Boolean
	
	operation flagAsPartiallyInitialized() is do
		_isPartiallyInitialized:=true
	end
	
	operation removeFlagAsPartiallyInitialized() is do
		_isPartiallyInitialized:=void 
	end
	
	property readonly isPartiallyInitialized : Boolean
	getter is do
		result:= _isPartiallyInitialized==true //Note that void!=true
	end

	operation merge(stepToMerge:IwStep) is do
		mergeName(stepToMerge)
		nodes.addAll(stepToMerge.nodes)

		//stle: remove all pointers, requires dry + test		
		stepToMerge.nodes.clear
		stepToMerge.concern:=void
		stepToMerge.outboundStubs.clear
	end
	
	operation mergeName(stepToMerge:IwStep) is do
		if(name<stepToMerge.name) then
			name:=combineName(name,stepToMerge.name)
		else
			name:=combineName(stepToMerge.name,name)
		end
	end
	
	operation combineName(name1:String,name2:String):String is do
		result:=name1+"_"+name2
	end
}

aspect class IwStartPoint{
	operation linkStep() is do
		if(startPointType!="bound") then
			var currentStep:IwStep init IwStep.new
			currentStep.flagAsPartiallyInitialized
			concern.steps.add(currentStep)
			step_DeepFirstSearch(currentStep)
		end
	end
	
	operation linkStepOfAnotherConcern() is do
		var currentStep:IwStep init IwStep.new
		currentStep.name:=name //Here the step as the startPoint name instead of an input name
		concern.steps.add(currentStep)
		step_DeepFirstSearch(currentStep)
	end
}

aspect class IwInput {
	method explore(currentStep:IwStep) is do
		if(currentStep.isPartiallyInitialized) then
			currentStep.name:=name
			currentStep.removeFlagAsPartiallyInitialized
		else
			currentStep:=IwStep.new
			currentStep.name:=name
			concern.steps.add(currentStep)
		end
		super(currentStep)
	end
}

aspect class IwEndPoint
{
	method explore(currentStep:IwStep) is do
		step:=currentStep
		outBindings.each{outBinding|outBinding.explore(currentStep)}
	end
}

aspect class IwOutBinding
{
	operation explore(currentStep:IwStep) is do
		if(isOutboundStubInSameConcern) then
			currentStep.outboundStubs.add(pluginBinding.stub)
			stubExit.target.step_DeepFirstSearch(currentStep)
		end
	end
	
	property readonly isOutboundStubInSameConcern : Boolean
	getter is do
		result:=pluginEndPoint.concern==pluginBinding.stub.workflow.concern
	end
}

aspect class IwStub{
	method explore(currentStep:IwStep) is do
		step:=currentStep
		
		if(isPluginInSameConcern) then 
			staticPluginBinding.inBindings.each{inBinding|inBinding.pluginStartPoint.step_DeepFirstSearch(step)}
		else
			staticPluginBinding.inBindings.each{inBinding|inBinding.pluginStartPoint.linkStepOfAnotherConcern}
			super(currentStep)//explore as a normal node
		end
	end

	property readonly isPluginInSameConcern : Boolean
	getter is do
		//All inBinding must refers to the same plugin
		result:=workflow.concern==staticPluginBinding.inBindings.one.pluginStartPoint.workflow.concern
	end
	
	property readonly staticPluginBinding : IwPluginBinding
	getter is do
		//stle:Single again
		result:=pluginBindings.one
	end
	
}

